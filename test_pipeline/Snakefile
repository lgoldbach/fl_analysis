# General Snakefile for simulating kimura adaptive walks

configfile: "./config.yaml"
scripts = config["scripts"]
csv_input = config["csv_input"]


rule all:
    input:
        expand("transition_matrix_10-{pop_size_exp}.npz", 
               pop_size_exp=config["pop_size_exp"])


rule parse_genotypes:
    input:
        csv_input
    output:
        genotypes="genotypes.npy",
        genotypes_num="genotypes.num.npy"
    params:
        csv_delimiter=config["csv_delimiter"]
    script:
        scripts + "extract_genotypes.py"

rule parse_phenotypes:
    input:
        csv_input
    output:
        phenotype="phenotypes.npy"
    params:
        csv_delimiter=config["csv_delimiter"],
        csv_pheno_id="score"
    script:
        scripts + "extract_phenotypes.py"


rule compute_hamming_distances:
    input:
        "genotypes.num.npy"
    output:
        "hamming_distances.npy"
    script:
        scripts + "compute_hamming_distances.py"
    

rule build_transition_matrix:
    input: 
        hamming_dist="hamming_distances.npy",
        phenotypes="phenotypes.npy",
        genotypes="genotypes.npy"
    output:
        "transition_matrix_10-{pop_size_exp}.npz"
    params:
        pop_size_exp="{pop_size_exp}"
    script:
        scripts + "build_transition_matrix_uniform.py"
